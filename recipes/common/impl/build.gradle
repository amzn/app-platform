plugins {
    id 'software.amazon.app.platform'

    // This plugin lives in the buildSrc directory and is not published.
    // It's used to manage certain configurations and dependencies for all
    // :recipes:* modules, otherwise we'd need to repeat them several times.
    id 'software.amazon.app.platform.lib'
}

appPlatform {
    enableComposeUi true
    enableKotlinInject true
    enableModuleStructure true
    enableMoleculePresenters true
}

kotlin {
    def noAndroid = sourceSets.create("noAndroidMain")
    def noIos = sourceSets.create("noIosMain")

    sourceSets.named('commonMain').configure {
        noAndroid.dependsOn(it)
        noIos.dependsOn(it)
    }
    sourceSets.named('appleMain').configure {
        it.dependsOn(noAndroid)
    }
    sourceSets.named('wasmJsMain').configure {
        it.dependsOn(noAndroid)
        it.dependsOn(noIos)
    }
    sourceSets.named('desktopMain').configure {
        it.dependsOn(noAndroid)
        it.dependsOn(noIos)
    }
    sourceSets.named('androidMain').configure {
        it.dependsOn(noIos)
    }
}

dependencies {
    commonMainImplementation compose.components.resources
    commonMainImplementation compose.material3
    commonMainImplementation compose.materialIconsExtended
    commonMainImplementation compose.runtimeSaveable
    commonMainImplementation compose.ui

    //noinspection UseTomlInstead
    commonMainImplementation("co.touchlab:kermit:2.0.4")

    commonMainImplementation libs.androidx.collection

    androidMainImplementation libs.navigation3.runtime
    androidMainImplementation libs.navigation3.ui
}
